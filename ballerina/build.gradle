/*
 * Copyright (c) 2023, WSO2 LLC. (http://www.wso2.org)
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'io.ballerina.plugin'
}

description = 'Ballerina - Redis Ballerina Connector'

def packageName = "redis"
def packageOrg = "ballerinax"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

ballerina {
    packageOrganization = packageOrg
    module = packageName
    testCoverageParam = "--code-coverage --coverage-format=xml"
    isConnector = true
    platform = "java17"
}

configurations {
    externalJars
}

dependencies {
    externalJars(group: 'com.google.guava', name: 'guava', version: "${guavaVersion}") {transitive = false}
    externalJars(group: 'commons-io', name: 'commons-io', version: "${commonsIoVersion}") {transitive = false}
    externalJars(group: 'io.netty', name: 'netty-common', version: "${nettyVersion}") {transitive = false}
    externalJars(group: 'io.netty', name: 'netty-buffer', version: "${nettyVersion}") {transitive = false}
    externalJars(group: 'io.netty', name: 'netty-transport', version: "${nettyVersion}") {transitive = false}
    externalJars(group: 'io.netty', name: 'netty-resolver', version: "${nettyVersion}") {transitive = false}
    externalJars(group: 'io.netty', name: 'netty-handler', version: "${nettyVersion}") {transitive = false}
    externalJars(group: 'io.netty', name: 'netty-transport-native-kqueue', version: "${nettyVersion}") {transitive = false}
    externalJars(group: 'io.netty', name: 'netty-transport-native-epoll', version: "${nettyVersion}") {transitive = false}
    externalJars(group: 'io.netty', name: 'netty-codec', version: "${nettyVersion}") {transitive = false}
    externalJars(group: 'io.projectreactor', name: 'reactor-core', version: "${reactorCoreVersion}") {transitive = false}
    externalJars(group: 'org.reactivestreams', name: 'reactive-streams', version: "${reactiveStreamsVersion}") {transitive = false}
}

task updateTomlFiles {
    doLast {
        def newBallerinaToml = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        newBallerinaToml = newBallerinaToml.replace("@toml.version@", tomlVersion)
        newBallerinaToml = newBallerinaToml.replace("@lettuce.version@", lettuceCoreVersion)
        newBallerinaToml = newBallerinaToml.replace("@commons.pool2.version@", commonsPool2Version)
        newBallerinaToml = newBallerinaToml.replace("@guava.version@", guavaVersion)
        newBallerinaToml = newBallerinaToml.replace("@commons.io.version@", commonsIoVersion)
        newBallerinaToml = newBallerinaToml.replace("@netty.version@", nettyVersion)
        newBallerinaToml = newBallerinaToml.replace("@reactor-core.version@", reactorCoreVersion)
        newBallerinaToml = newBallerinaToml.replace("@reactive-streams.version@", reactiveStreamsVersion)
        ballerinaTomlFile.text = newBallerinaToml
    }
}

task commitTomlFiles {
    doLast {
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update the toml files\" Ballerina.toml Dependencies.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the toml files' Ballerina.toml Dependencies.toml"
            }
        }
    }
}

task startRedisServer() {
    doLast {
        println("Starting redis server...")

        def dockerComposeFile = "docker-compose-standalone.yml"
        if (System.getenv("TEST_CLUSTER_MODE")?.toBoolean()) {
            dockerComposeFile = "docker-compose-cluster.yml"
        }

        exec {
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "docker-compose -f tests/resources/docker/${dockerComposeFile} up && exit %%ERRORLEVEL%%"
            } else {
                commandLine 'sh', '-c', "docker-compose -f tests/resources/docker/${dockerComposeFile} up -d"
            }
        }
    }
}

task stopRedisServer() {

    doLast {
        println("Stopping redis server...")

        def dockerComposeFile = "docker-compose-standalone.yml"
        if (System.getenv("TEST_CLUSTER_MODE")?.toBoolean()) {
            dockerComposeFile = "docker-compose-cluster.yml"
        }

        exec {
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "docker-compose -f tests/resources/docker/${dockerComposeFile} down && exit %%ERRORLEVEL%%"
            } else {
                commandLine 'sh', '-c', "docker-compose -f tests/resources/docker/${dockerComposeFile} down"
            }
        }
    }
}

build {
    dependsOn copyToLib
    dependsOn ":${project.packageName}-native:build"
    dependsOn startRedisServer
    finalizedBy stopRedisServer
}

test {
    dependsOn ":${project.packageName}-native:build"
    dependsOn startRedisServer
    finalizedBy stopRedisServer
}

clean {
    delete 'build'
}

publishToMavenLocal.dependsOn build
publish.dependsOn build
